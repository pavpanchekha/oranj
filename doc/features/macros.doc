Macros
======

What Are Macros?
----------------

Macros are an extremely powerful feature of oranj. At its most
basic, a macro can be viewed simply as a more-general function,
where the generality comes from the fact that a macros arguments
are not evaluated. However, despite this seeming simplicity,
macros can lead to extremely powerful generalizations and abstractions.

Macros (in the sense oranj uses) appeared first in the language lisp,
which adherents call the most powerful and elegant language in the
world. There's something to this. In fact, macros do not appear in
any language but lisp (until now, in oranj) and so there is some
danger in having oranj considered a new dialect of lisp as opposed
to a language of its own.

As already mentioned, the main difference between a macro and a
function is that a macro does not have its argument evaluated
before the macro is called.

::heading
A Simple Macro

::oranj::
oranj> show_ast = fn(x) is Macro {return x}
oranj> show_ast(a + 1)
['OP1', '+', ['IDENT', 'a'], ['PRIMITIVE', ('INT', '1', 10)]]

Above is a very basic macro definition. By adding the <code>`Macro</code
tag to our function, it becomes a macro. As such, when we call it later,
it is passed not the <em>value</em> of <code>a + 1</code>, but the parse
tree to it.

Why is this useful? The most basic way you might use a macro is for a
swap function. To do this, however, we need some way to set values
outside the scope of our macro. There are two ways to do this.

::heading::
<code>swap</code> and Direct Access to <code>intp</code>

The first is simply to use the <code>intp</code> variable to access the
raw interpreter object and set the values there. This can be simplified
by instead using the built-in <code>macros</code> library:

::oranj::
import macros
swap = fn(a, b) is Macro {
    i = macros.Interpreter(intp)
    val1 = i.run(a)
    val2 = i.run(b)

    ident1 = a[1]
    ident2 = b[1]
    i.set(ident1, val2)
    i.set(ident2, val1)
}

There are, however, several problems with this. The first is the lack of
any error correction: what if the arguments passed were not simple
identifiers, but instead some sort of operator? Then we'd be trying to
set the value of "<code>+</code>", for example. We'll see how to use
the <code>macros</code> library’s tree-parsing facilities to make this
easier.

::heading::
<code>`FullMacro</code>

The other option for a swap function is to simply ask the interpreter
to evaluate a some code in the context of the calling function. To make
this easy, the <code>`FullMacro</code> tag exists, which returns not some
specific value but a parse tree, which the interpreter then executes.

::oranj::
import macros
swap = fn(a, b) is FullMacro {
    i = macros.Interpreter(intp)
    val1 = i.run(a)
    val2 = i.run(b)
    return [["ASSIGN1", a[1], ["VALUE", val2]], ["ASSIGN1", b[1], ["VALUE", val1]]]}
}

Note the use of the <code>"VALUE"</code> type of parse tree node to pass
a raw, precomputed value to the oranj interpreter.

Tree Parsing
------------

In the <code>swap</code> example above, we had a problem in checking
whether the arguments passed to us were really <code>"IDENT"</code>
nodes. The <code>macros</code> library includes the <code>match</code>
function for just this purpose.

First, let’s try this function on normal lists, and then discuss how to
use it in macros.

::oranj::
oranj> import macros
oranj> macros.match(["Element1", 1, 2, macros.val("x")], ["Element1", 1, 2, 3])
["x": 3]

What the <code>macros.match</code> function did was use a template (the
first argument) to extract information from a specific list. Now, what
is that “<code>macros.val("x")</code>”?

::heading::
Extractors

::oranj::
oranj> import macros
oranj> x = macros.val("x")
oranj> x
??x

Huh? <code>??x</code>? Two question marks indeed. Well, its simple,
really. To continue from the previous example:

::oranj::
oranj> x is `Extractor
true
oranj> x << 3
3
oranj> x << ["Lots", ["of", ["complicated": "stuff"]]]
['Lots', ['of', ['complicated': 'stuff']]]

So really, a <code>macros.val</code> object is just some object with
the <code>`Extractor</code> flag and an overridden funnel (a.k.a. output)
operator. Oh, and the object has a name parameter:

::oranj::
oranj> x.name
'x'

Now, what is the point of adding a name? Because when
<code>macros.match</code> is parsing your list, it will funnel the data
that is in the place of an <code>`Extractor</code> object, and store it
in a dictionary under that object’ s name. That dictionary is eventually
returned to you.

::heading::
<code>macros.match</code> and Macros

::oranj::
import macros
swap = fn(a, b) is Macro {
    i = macros.Interpreter(intp)
    
    argparse = macros.match([["IDENT", macros.val("a")], ["IDENT", macros.val("b")]], [a, b])
    ident1 = argparse["a"]
    ident2 = argparse["b"]
    
    val1 = i.run(a)
    val2 = i.run(b)
    
    i.set(ident1, val2)
    i.set(ident2, val1)
}

You can see how we've now extracted the relevant parts of the arguments,
without having to do it manually. In fact, <code>macros.match</code> will
even raise an <code>AssertionError</code> if the tree it is given doesn’t
match the template, so we can even claim some amount of error checking.

Common Macro Bugs
-----------------

Well, you can't expect to write perfect code. And due to their lower level,
macros possess several unique types of bugs.

::heading::
Variable Capture

Mostly prevalent in <code>`FullMacro</code>s, this happens when an internal
variable has the same name as an external one, and so gets overridden. Take,
for example, the following attempt at writing <code>swap</code> as a
<code>`FullMacro</code>:

::oranj::
swap = fn(a, b) is FullMacro {
    return [["ASSIGN1", "t", a],
        ["ASSIGN1", a[1], b],
        ["ASSIGN1", b[1], ["IDENT", "t"]]
}

This will appear to work, but at some point, disaster will strike:

::oranj::
oranj> a, b, t = 1, 2, 3
oranj> swap(a, b)
oranj> a
2
oranj> b
1
oranj> t
1

Oh no! Our macro overwrote the definition of <code>t</code>! How do
we fix this? Well, we could always rename <code>t</code> to something
more obscure, like “<code>xasdf235havidu<code>,” but this isn't really
a fix so much as a patch. It’s still possible to overwrite a variable
this way, just far less likely. The solution is to generate a symbol
no one else will use. For this purpose, the <code>macros</code> library
includes the <code>gensym</code> function.

::oranj::
oranj> import macros
oranj> macros.gensym()
"!!0"
oranj> macros.gensym()
"!!1"

“Those aren't valid identifiers,” you say. And you're right. But the
oranj interpreter doesn't actually require the identifier to be a valid
one. In fact, it stores some internal state in such “illegal” variables.
All <code>gensym</code> does is generate such an “illegal” variable,
each time naming it differently (it just increments a counter). So,
every time your macro is invoked, you get a shiny new temporary variable
that interferes with nothing. So, the above <code>swap</code> macro could
be written as:

::oranj::
import macros
swap = fn(a, b) is FullMacro {
    t = macros.gensym()
    return [["ASSIGN1", t, a],
        ["ASSIGN1", a[1], b],
        ["ASSIGN1", b[1], ["IDENT", t]]
}

Now, there is no danger of interference, because it would be impossible to
define the variable <code>!!xxx</code> (unless one actively tried to, in
which case they deserve everything they get).
