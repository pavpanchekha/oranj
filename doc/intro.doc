A brief introduction
====================

Abstract
--------

::comment::
’ “ ”

All right, so you want to learn oranj. Fine. Just remember that oranj
is not yet done, and so you may at times run into bugs and
errors. Don't worry, they’ll go away soon. In fact, they’ll go away
even sooner if you <a href="http://github.com/pavpanchekha/oranj/issues">
report bugs</a>. But that’s already getting off track.

Why learn oranj? Well, it’s certainly fun. That was a good enough
reason for me to write it. Don’t like it? Well, think of your own
reason, there are plenty out there.

This brief introduction is intended for programmers. If this is your
first language, feel free to read through, but you might be a bit
confused.

To install oranj, you’ll want to head over to the <a href="install.html">
installation page</a>.

Beginning
---------

::heading::
Input, output

::oranj::
io << "Hello, World" << endl

The standard prayer to the programming gods thus completed, we
can move on to other ways of doing input and output.

::oranj::
output("I can say hello, too")

Note that you don’t need a newline at the end. What about input?

::oranj::
name = input("What is your name? ")

With just a prompt, input will return a string, which you store in
name. Now, if you wanted an integer, you might...

::oranj::
name = io << "What is your age? " >> int

A second argument to input would to the trick too:

::oranj::
name = input("What is your age? ", int)

In general, <code>input</code> and <code>output</code> (as well as
their sister function <code>error</code>, which prints to what C types
would call “stderr”) give you more options, while using
<code>io</code> is easier to type and makes C++ programmers happy.

While we’re on the topic, let’s cover files:

::oranj::
f = file("test", "w")
f.write("Test data")

The <code>"w"</code> make the file be opened for writing as
opposed to the default, reading (you can also do <code>"a"</code>
to append. Of course, that’s a rather silly way of doing
things. There are better ones...

::oranj::
f = file("test", "w")
io.bind(f)
io << "Hello from io" << endl

<code>io</code> has this useful function called <code>bind</code>,
which will make <code>io</code> (and <code>output</code>, and
<code>input</code>, and <code>error</code>) use the variable passed
for input and output. For example, if you’re using oranj to serve web
pages, <code>io</code> wouldn’t be the terminal (or your server’s
log), but instead the HTTP stream (actually, <code>error</code> would
probably be your server’s log file).

::heading::
Math

::oranj::
io << 1+2*3/4^5 + 12 000 << endl # Prints 12001.005859

Addition, multiplication, division, exponentiation, and all of their
friends work exactly as expected. Note that you can put spaces into a
number for added readability. Also note that division is
floating-point if necessary. Actually, on the subject of
floating-point...

::oranj::
io << 1/3 << endl # Gives 0.333333
io << (1/3) * 3 << endl # Gives 1

Floating points will also upconvert (if within 28 decimal points of
precision. At times, this isn’t enough precision, but it’s usually far
more than needed.

::oranj::
io << 17 mod 4 << endl # Gives 1
io << 5|15 << endl # Gives True

<code>mod</code> is used for modular arithmetic, <code>|</code> is
used for divisibility-testing (above, it’s asking if 5 divides
15 evenly).

::oranj::
io << inf + 1 << endl # inf

Yes, infinity plus one is infinity

::heading::
Loops and Such

::oranj::
if inf < 0 {io << "Uh oh!" << endl}

<code>if</code> statements and such don’t require parentheses. In
exchange, the braces are required, even if its a one-liner. The same
holds for <code>while</code> and <code>for</code> statements.

::oranj::
if a == b {
    io <<; "Equal"
} elif a > b {
    io << "Greater"
} else {
    io << "Smaller"
}
io << endl

<code>elif</code> and <code>else</code> work as you might
expect. <code>elif</code> is short for “else if” and basically acts as
a second <code>if</code> statement that only runs if the first
didn’t. <code>else</code> is what happens when nothing else does.

::oranj::
a, b = 0, 1
while a < 1000 {
    a, b = b, a+b
    io << a << endl
}

The fibonacci numbers always seem the first example of while
statements... I wonder why. In any case, <code>while</code> loops
are very similar to C <code>while</code> loops, only you don’t
need the parentheses. Oh, and you can’t do one-liners.

::cpp::
while (a < 10) a++; // Doesn't work in oranj

But hey, its often bad programming style, might annoy you later
on when you actually do need to add braces, and saves you two key
presses (the parentheses) most of the time.

::oranj::
for a in range(10) {
    io << a^2 << " "
}
io << endl

<code>for</code> loops work much like they do in Python: they
iterate over a list. In this case, the list is
<code>range(10)</code>, which returns <code>[0, 1, 2, 3, 4, 5, 6,
  7, 8, 9]</code> (note that 0 is in the range, but 10 is not). You
can use any other list instead:

::oranj::
for a in [2, 3, 5, 7, 11] {
    io << "Prime! "
}
io << endl

In fact, there’s a shortcut if you just want to loop a set number of
times:

::oranj::
for i in 20 {io << i^2 << endl}

This works because any integer can work as a list, returning the same
values as would a <code>range()</code> of itself.

Functions
---------

::heading::
Basic Functions

::oranj::
hi = fn{io << "Hello, World!" << endl}
hi()

The simplest functions take nothing and give nothing. If you want to
pass arguments...

::oranj::
square = fn(x) {return x^2}

This function takes one argument (<code>x</code>) and returns its
square. Now, there are two ways of calling this function

::oranj::
square(4) == 4 ! square

The difference is that the second focuses on the value <code>4</code>,
whereas the other on the operation, <code>square</code>. Its sort of
like the distinction between passive and active voice. While “The
firemen saved the boy” and “The boy was saved by the firemen” mean the
same thing, you would use them in different circumstances. Now, what
if you want multiple arguments?

::oranj::
sub = fn "Subtract two numbers" (x, y=1) {
    return x - y
}

Note that the argument <code>y</code> has a default value,
<code>2</code>, and that the function has a docstring, which is a
short description of what the function does. The key word here is
“short”: you shouldn’t describe each argument or the output
format; that should go in the documentation. Instead, you should just
specify the action so that someone reading your code knows what the
function does.

::oranj::
io << (2 ! sub(4)) << endl # Output: -2<

In this case, the <strong>first</strong> argument is
<code>2</code>, not the second. When using the alternative
function call syntax, the argument before the exclamation mark
is always the first argument. (What if you want it to be second?
Then you probably don't want to use the alternative syntax.)

::heading::
Functions Making Functions

::oranj::
adder = fn(x) {
    return fn(y) {return x + y}
}

The function <code>adder</code> returns a function. That's right, it’s
a function that returns a function. In this case, the function it
returns has some values “bound”: the variable <code>x</code> is
constant between calls of the internal function. How would you use
this function-of-functions?

::oranj::
add10 = adder(10)
io &lt;&lt; add10(7) &lt;&lt; endl # Outputs 17
io &lt;&lt; adder(2)(15) &lt;&lt; endl # Outputs 17

Remember that there's nothing really strange going on; you’re just
creating a function, which you can then assign to a variable or call
like any other function. You can, of course use this technique to do
far more complicated things, such as hiding variables where no one can
find them (make them local variables of some particular function that
creates other functions with the variables &#8220;embedded&#8221;
within) or for using software patterns like the singleton.

On the other hand, don&#8217;t overuse this feature. Every time you
dynamically create a function, you're using up memory. The
<code>adder</code> example is usually (though not always; sometimes
you need a one-argument function) better written as a function of two
arguments.

Remember that in programming, cleverness is often a bad thing, not a
good one.

::heading::
Functions of Functions

::oranj::
apply_and_print = fn "Apply function to elements of a list" (l, f) {
    for el in l {
        io << f(el) << " "
    }

    io << endl
}

There’s no reason why a function can’t take another function as an
argument. After all, what would we do with all of the functions we’ve
been making? This example gives the basic idea. We can combine making
and taking functions, too:

::oranj::
first_arg = fn "Set first argument of a function" (f, arg) {
    return fn(*args, **kwargs) {
        return f(arg, *args, **kwargs)
    }
}

The idea here should be rather simple: we create a new function
which calls the one we were passed, but inserts in a new argument
in front. The one possibly confusing thing here is the new syntax.
Specifying an asterisk before an argument name (which
<strong>should</strong> be <code>args</code>) makes that argument be a
list of all other arguments (other as in not already used). Two
askerists make it a dictionary of all other keyword arguments. This
lets you create functions with arbitrary numbers of arguments.
